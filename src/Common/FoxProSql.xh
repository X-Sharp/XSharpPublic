///////////////////////////////////////////////////////////////////////////
// FoxProSql.xh
//
// XSharp FoxPro SQL Preprocessor Directives
//
// Copyright (c) XSharp BV.  All Rights Reserved.  Licensed under the Apache License, Version 2.0.
// See License.txt in the project root for license information.
//
// Caution: do not modify this file. It will be overwritten during product updates
//


#ifndef FOXPROSQL_XH
#define FOXPROSQL_XH


#command __NOFOXPROSQL__ <any> => #warning This command is not (yet) supported: <(any)>

// DDL Commands

// SQL Alter Commands
#command ALTER TABLE <*clauses*>                            => __SqlAlterTable(<"udc">)
#command ALTER TABLE <*clauses*> [COLUMN]  <*moreclause*>   => __SqlAlterTable(<"udc">)

// SQL Create Commands
#command CREATE <tbl:TABLE,DBF> <*clauses*>                 => __SqlCreateTable(<"udc">)
#command CREATE CURSOR <*clauses*>                          => __SqlCreateCursor(<"udc">)
#command CREATE [SQL] VIEW <(cView)> [<remote:REMOTE>] ;
    [CONNECTION <(cConn)>] [<share:SHARE>] ;
    [AS  <*sqlstatement*>]                                  => __NOFOXPROSQL__ <(udc)> //  XSharp.RDD.Dbc.CreateView( <(cView)>, <cConn>, <.remote.>, <.share.>, <(sqlstatement)>)

// SQL Drop Commands
#command DROP TABLE <*clauses*>                             => __NOFOXPROSQL__ <(udc)> // __SqlDropTable(<"udc">)
#command DROP VIEW <*clauses*>                              => __NOFOXPROSQL__ <(udc)> // __SqlDropView(<"udc">)


// DML Commands

// SQL Insert commands
// First is generic INSERT, to enable keyword coloring  while typing

#command INSERT [INTO] [<*any*>] => #error <(udc)> : Missing FROM or VALUES clause

#command INSERT INTO <(table)> FROM MEMVAR => ;
    __SqlInsertMemVar(<"table"> )

#command INSERT INTO <(table)> [( <(f1)> [, <(fn)> ] )]  VALUES (  <(v1)> [, <(vn)> ] )     => ;
    __SqlInsertValues(<"table"> , { <"f1"> [, <"fn"> ] },  { <v1> [, <vn> ] } )

#command INSERT INTO <(table)>  FROM ARRAY <ArrayName>   => ;
    __SqlInsertFromArray( <"table">,  <ArrayName>)

#command INSERT INTO <(table)> FROM NAME <ObjectName>   => ;
    __SqlInsertFromObject(<"table"> , <ObjectName> )

#command INSERT INTO <(table)> [( <(f1)> [, <(fn)> ] )] <s:Select> <*moreclauses*>   => ;
    __NOFOXPROSQL__ <(udc)>  // __SqlInsertFromSQL(<"udc">, <"table"> , { <"f1"> [, <"fn"> ] },  <"s">+" "+<"moreclauses"> )


// SQL Delete
#command DELETE [<*tableName*>] FROM  <*clauses*>           => __NOFOXPROSQL__ <(udc)> //  __SqlDelete(<"udc">)
#command DELETE TRIGGER  <*clauses*>                        => #error "DELETE TRIGGER is not supported "

// SQL Update Commands
#command UPDATE [<*tableName*>] SET <*setclauses*> ;
  [FROM <*fromclause*>] [JOIN <*joinclause*>]  ;
  [WHERE <*whereclause*>]                                   => __NOFOXPROSQL__ <(udc)> // __SqlUpdate(<"udc">)

// SQL Select commands. Separate commands for TO and 3 different INTO variations


#command SELECT <*columnClauses*> FROM <*TableList*>           ;
    [WITH (BUFFERING = <lExpr> ) ]                             ;
    [WHERE <*WhereConditions*> ]                               ;
    [GROUP BY <*ColumnList*> [HAVING <*HavingConditions*>] ]   ;
    [UNION [ALL] <*SelectCommand*>]                            ;
    [ORDER BY <*orderByList*>]                                 ;
    [TO <dest:FILE,PRINTER,SCREEN> <*extraoptions*>]        => ;
    __NOFOXPROSQL__ <(udc)> //__FoxSqlSelect(<"udc">,<"dest">,<"extraoptions">

#command SELECT <*columnClauses*> FROM <*TableList*>           ;
    [WITH (BUFFERING = <lExpr> ) ]                             ;
    [WHERE <*WhereConditions*> ]                               ;
    [GROUP BY <*ColumnList*> [HAVING <*HavingConditions*>] ]   ;
    [UNION [ALL] <*SelectCommand*>]                            ;
    [ORDER BY <*orderByList*>]                                 ;
    INTO <dest:ARRAY,CURSOR,TABLE> <NAME> ;
	[<cursoropt:NOFILTER,READWRITE>]  ;
	[DATABASE <DatabaseName> [NAME <LongName>] ] =>;
    __NOFOXPROSQL__ <(udc)>  // __FoxSqlSelectSpecial(<"udc">, <(dest)>, <(Name)>, <cursoropt>,<DatabaseName>,<LongName>)

   #endif
// EOF