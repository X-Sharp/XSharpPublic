///////////////////////////////////////////////////////////////////////////
// DbCmds.xh
//
// XSharp Database commands
//
// Copyright (c) XSharp BV.  All Rights Reserved.  Licensed under the Apache License, Version 2.0.
// See License.txt in the project root for license information.
//
// IMPORTANT:  Functions beginning with an underscore
//             character are reserved, version-dependent functions.
//             These functions should not be called directly.
//
// Caution: do not modify this file. It will be overwritten during product updates
// Rules with more optional clauses should precede rules without these optional clauses, such as INDEX ON, REPLACE and DELETE
//

#ifndef DBCMD_XH

#define DBCMD_XH

// rule for Fields, used in several other rules

#translate __DBFIELDS__ <f1> [,<fn> ]   => ;
           __DBFIELDLIST__  {<(f1)>[, <(fn)> ]}


****
*  DB
*

#command SELECT <whatever>              => DbSelectArea( <(whatever)> )
#command SELECT <f>([<list,...>])       => DbSelectArea( <f>(<list>) )
#command SELECT &<whatever>             => DbSelectArea( <whatever> )

#command USE                            => DbCloseArea()

#command USE <(db)>                                                     ;
             [VIA <rdd>]                                                ;
             [ALIAS <a>]                                                ;
             [<new: NEW>]                                               ;
             [<ex: EXCLUSIVE>]                                          ;
             [<sh: SHARED>]                                             ;
             [<ro: READONLY,NOUPDATE>]                                  ;
             [INDEX <(index1)> [, <(indexn)>]]                          ;
                                                                        ;
      => DbUseArea(                                                     ;
                    <.new.>, <rdd>, <(db)>, <(a)>,                      ;
                    if(<.sh.> .or. <.ex.>, !<.ex.>, NIL), <.ro.>        ;
                  )                                                     ;
                                                                        ;
      [; DbSetIndex( <(index1)> )]                                      ;
      [; DbSetIndex( <(indexn)> )]


#command SET INDEX TO [ <(index1)> [, <(indexn)>]]                      ;
                                                                        ;
      => DbClearIndex()                                                 ;
                                                                        ;
      [; DbSetIndex( <(index1)> )]                                      ;
      [; DbSetIndex( <(indexn)> )]

#command INDEX ON <key>         										;
     TAG <order>                										;
     [TO <file>]                										;
     [FOR <fo>]                 										;
     [<all:ALL>]                										;
     [WHILE <wh>]               										;
     [NEXT <nx>]                										;
     [RECORD <rec>]             										;
     [<rs:REST>]                										;
     [EVAL <ev>]                										;
     [EVERY <evr>]              										;
     [<u:UNIQUE>]               										;
     [<asd:ASCENDING>]          										;
     [<dsd:DESCENDING>]         										;
     [<lCur:USECURRENT>]        										;
     [<lAdd:ADDITIVE>]          										;
     [<lCus:CUSTOM>]            										;
     [<lNop:NOOPTIMIZE>]        										;
    => OrdCondSet( <"fo">, , [<.all.>], <{wh}>, <{ev}>, <evr>, 			;
          RecNo(), <nx>, <rec>,[<.rs.>],[<.dsd.>],<.lAdd.>,					;
          [<.lCur.>], [<.lCus.>], [<.lNop.>] )      						;;  ;
       OrdCreate(<(file)>,<(order)>,<(key)>,,IF(<.u.>,.T., NIL))

#command INDEX ON <key> TO <file> [<u:UNIQUE>]  						;
      =>  OrdCondSet()  ;;        										;
          DbCreateIndex(<(file)>,<(key)>,,IIF(<.u.>, .T., NIL))

#command REINDEX                => DbReindex()
#command SET ORDER TO <n>       => DbSetOrder( <n> )
#command SET ORDER TO           => DbSetOrder(0)

#command APPEND BLANK           				=> DbAppend()
#command PACK                   				=> DbPack()
#command ZAP                    				=> DbZap()
#command UNLOCK [RECORD <rcd>]  				=> DbUnlock(<rcd>)
#command UNLOCK ALL             				=> DbUnlockAll()
#command COMMIT                 				=> DbCommitAll()

#command GOTO <n>               => DbGoto(<n>)
#command GO <n>                 => DbGoto(<n>)
#command GOTO TOP               => DbGoTop()
#command GO TOP                 => DbGoTop()
#command GOTO BOTTOM            => DbGoBottom()
#command GO BOTTOM              => DbGoBottom()

#command SKIP                   => DbSkip(1)
#command SKIP <n>               => DbSkip( <n> )

#command SEEK <xpr>  [<soft:SOFTSEEK>] [<lst:LAST>]  => dbSeek( <xpr> , <.soft.>, <.lst.>)

#command SET FILTER TO          => dbClearFilter()
#command SET FILTER TO <xpr>    => dbSetFilter( <{xpr}>, <"xpr"> )

#command SET FILTER TO <x:&>                                            ;
      => IF ( Empty(<(x)>) )                                            ;
       ;    DbClearFilter()                                             ;
       ; ELSE                                                           ;
       ;    DbSetFilter( <{x}>, <(x)> )                                 ;
       ; END

#command REPLACE [ <f1> WITH <x1> [, <fn> WITH <xn>] ]                  ;
         [FOR <lfor>]                                                    ;
         [WHILE <lwhile>]                                                ;
         [NEXT <nnext>]                                                  ;
         [RECORD <rec>]                                                 ;
         [<rest:REST>]                                                  ;
         [<noopt: NOOPTIMIZE>]                                          ;
         [ALL]                                                          ;
                                                                        ;
      => DbEval(                                                        ;
                 {||DbAutoLock(), _FIELD-><f1> := <x1> [, _FIELD-><fn> := <xn>], DbAutoUnLock()},    ;
                 <{lfor}>, <{lwhile}>, <nnext>, <rec>, <.rest.>, <.noopt.>;
               )


#command REPLACE <f1> WITH <v1> [, <fN> WITH <vN> ]                     ;
      => DbAutoLock(); _FIELD-><f1> := <v1> [; _FIELD-><fN> := <vN>]; DbAutoUnLock()

#command DELETE                                                         ;
         [FOR <lfor>]                                                   ;
         [WHILE <lwhile>]                                               ;
         [NEXT <nnext>]                                                 ;
         [RECORD <rec>]                                                 ;
         [<rest:REST>]                                                  ;
         [<noopt: NOOPTIMIZE>]                                          ;
         [ALL]                                                          ;
                                                                        ;
      => DbEval(                                                        ;
                 {|| DbAutoLock(), DbDelete(), DbAutoUnLock()},                                       ;
                 <{lfor}>, <{lwhile}>, <nnext>, <rec>, <.rest.>, <.noopt.>;
               )

#command DELETE                 =>  DbAutoLock(); DbDelete(); DbAutoUnLock()

#command RECALL                                                         ;
         [FOR <lfor>]                                                   ;
         [WHILE <lwhile>]                                               ;
         [NEXT <nnext>]                                                 ;
         [RECORD <rec>]                                                 ;
         [<rest:REST>]                                                  ;
         [<noopt: NOOPTIMIZE>]                                          ;
         [ALL]                                                          ;
                                                                        ;
      => DBEval(                                                        ;
                 {|| DbAutoLock(), DbRecall(), DbAutoUnLock()},                                       ;
                 <{lfor}>, <{lwhile}>, <nnext>, <rec>, <.rest.>, <.noopt.>;
               )


#command RECALL                 =>  DbAutoLock(); DbRecall(); DbAutoUnLock()

#command FIND <*text*>          => DbSeek( <(text)> )
#command FIND := <xpr>          => ( find := <xpr> )
#command FIND = <xpr>           => ( find := <xpr> )

#command CONTINUE               => DbContinue()

#command LOCATE                                                         ;
         [FOR <lfor>]                                                   ;
         [WHILE <lwhile>]                                               ;
         [NEXT <nnext>]                                                 ;
         [RECORD <rec>]                                                 ;
         [<rest:REST>]                                                  ;
         [<noopt: NOOPTIMIZE>]                                          ;
         [ALL]                                                          ;
                                                                        ;
      => DbLocate( <{lfor}>, <{lwhile}>, <nnext>, <rec>, <.rest.>, <.noopt.> )

#command SET RELATION TO        => DbClearRelation()

#command SET RELATION                                                   ;
         [<add:ADDITIVE>]                                               ;
         [TO <key1> INTO <(alias1)> [, [TO] <keyn> INTO <(aliasn)>]]    ;
                                                                        ;
      => if ( !<.add.> )                                                ;
       ;    DbClearRelation()                                           ;
       ; END                                                            ;
                                                                        ;
       ; DbSetRelation( <(alias1)>, <{key1}>, <"key1"> )                ;
      [; DbSetRelation( <(aliasn)>, <{keyn}>, <"keyn"> )]

#command CREATE <(file1)>           ;
            [FROM <(file2)>]        ;
            [ALIAS <a>]             ;
            [VIA <rdd>]             ;
            [<NEW: NEW>]            ;
            => _DbCreate( <(file1)>, <(file2)> , <rdd>, <.new.>, <(a)> )


#command COPY STRUCTURE EXTENDED TO <(file)>                      		;
      => DbCopyXStruct( <(file)> )


#command COPY STRUCTURE TO <(file)> [__DBFIELDLIST__ <fields>]          ;
      => DbCopyStruct( <(file)>, <fields> )


#ifndef __DIALECT_FOXPRO__

#command COPY TO <(file)>   DELIMITED [WITH <*delim*>]                  ;
         [__DBFIELDLIST__ <fields>]                                     ;
         [FOR <lfor>]                                                   ;
         [WHILE <lwhile>]                                               ;
         [NEXT <nnext>]                                                 ;
         [RECORD <rec>]                                                 ;
         [<rest:REST>]                                                  ;
         [<noopt: NOOPTIMIZE>]                                          ;
         [ALL]                                                          ;
                                                                        ;
      => DbCopyDelim(                                                   ;
                        <(file)>, <(delim)>, <fields> ,                 ;
                        <{lfor}>, <{lwhile}>, <nnext>, <rec>, <.rest.>, <.noopt.>  ;
                      )


#command COPY TO <(file)> SDF                                           ;
         [__DBFIELDLIST__ <fields>]                                     ;
         [FOR <lfor>]                                                    ;
         [WHILE <lwhile>]                                                ;
         [NEXT <nnext>]                                                  ;
         [RECORD <rec>]                                                 ;
         [<rest:REST>]                                                  ;
         [<noopt: NOOPTIMIZE>]                                          ;
         [ALL]                                                          ;
                                                                        ;
      => DbCopySDF(                                                     ;
                      <(file)>, <fields>,                               ;
                      <{lfor}>, <{lwhile}>, <nnext>, <rec>, <.rest.>, <.noopt.>    ;
                    )

#command COPY TO <(file)>                                               ;
         [__DBFIELDLIST__ <fields>]                                     ;
         [FOR <lfor>]                                                    ;
         [WHILE <lwhile>]                                                ;
         [NEXT <nnext>]                                                  ;
         [RECORD <rec>]                                                 ;
         [<rest:REST>]                                                  ;
         [<noopt: NOOPTIMIZE>]                                          ;
         [ALL]                                                          ;
         [VIA <rdd>]                                                    ;
         [INHERIT <hdnlist,...>]                                        ;
                                                                        ;
      => DbCopy(                                                        ;
                   <(file)>, <fields>,                                  ;
                   <{lfor}>, <{lwhile}>, <nnext>, <rec>, <.rest.>       ;
                   <rdd>, { <(hdnlist)> }, <.noopt.>)


#command APPEND FROM <(file)> DELIMITED [WITH <*delim*>]                ;
         [__DBFIELDLIST__ <fields>]                                     ;
         [FOR <lfor>]                                                   ;
         [WHILE <lwhile>]                                               ;
         [NEXT <nnext>]                                                 ;
         [RECORD <rec>]                                                 ;
         [<rest:REST>]                                                  ;
         [ALL]                                                          ;
                                                                        ;
      => DbAppDelim(                                                    ;
                       <(file)>, <(delim)>, <fields>,                   ;
                       <{lfor}>, <{lwhile}>, <nnext>, <rec>, <.rest.>   ;
                     )


#command APPEND FROM <(file)> SDF                                       ;
         [__DBFIELDLIST__ <fields>]                                     ;
         [FOR <lfor>]                                                   ;
         [WHILE <lwhile>]                                               ;
         [NEXT <nnext>]                                                 ;
         [RECORD <rec>]                                                 ;
         [<rest:REST>]                                                  ;
         [ALL]                                                          ;
                                                                        ;
      => DbAppSDF(                                                      ;
                     <(file)>, <fields>,                                ;
                     <{lfor}>, <{lwhile}>, <nnext>, <rec>, <.rest.>     ;
                   )


#command APPEND FROM <(file)>                                          ;
         [__DBFIELDLIST__ <fields>]                                     ;
         [FOR <lfor>]                                                    ;
         [WHILE <lwhile>]                                                ;
         [NEXT <nnext>]                                                  ;
         [RECORD <rec>]                                                 ;
         [<rest:REST>]                                                  ;
         [ALL]                                                          ;
         [VIA <rdd>]                                                    ;
                                                                        ;
      => DbApp(                                                         ;
                  <(file)>, <fields>,                                   ;
                  <{lfor}>, <{lwhile}>, <nnext>, <rec>, <.rest.>, <rdd>    ;
                )

#command <cmd:COPY,APPEND>  <*clauses*> FIELDS <*moreclauses*> => ;
         <cmd> <clauses>   __DBFIELDS__ <moreclauses>


#endif

#command SORT  TO <(file)>  [ON <fields,...>]                           ;
         [FOR <lfor>]                                                    ;
         [WHILE <lwhile>]                                                ;
         [NEXT <nnext>]                                                  ;
         [RECORD <rec>]                                                 ;
         [<rest:REST>]                                                  ;
         [<noopt: NOOPTIMIZE>]                                          ;
         [ALL]                                                          ;
                                                                        ;
      => DbSort(                                                        ;
                   <(file)>, <fields>,                                  ;
                   <{lfor}>, <{lwhile}>, <nnext>, <rec>, <.rest.>, <.noopt.> ;
                 )


#command TOTAL TO <(file)> ON <key>                                     ;
         [__DBFIELDLIST__ <fields>]                                     ;
         [FOR <lfor>]                                                    ;
         [WHILE <lwhile>]                                                ;
         [NEXT <nnext>]                                                  ;
         [RECORD <rec>]                                                 ;
         [<rest:REST>]                                                  ;
         [<noopt: NOOPTIMIZE>]                                          ;
         [ALL]                                                          ;
                                                                        ;
      => DbTotal(                                                       ;
                    <(file)>, <{key}>, <fields>,                        ;
                    <{lfor}>, <{lwhile}>, <nnext>, <rec>, <.rest.>, <.noopt.>       ;
                  )

#command TOTAL <*clauses*> FIELDS <*moreclauses*> => ;
         TOTAL <clauses> __DBFIELDS__ <moreclauses>




#command UPDATE  FROM <(alias)>  [ON <key>]                             ;
         [REPLACE <f1> WITH <x1> [, <fn> WITH <xn>]]                    ;
         [<rand:RANDOM>]                                                ;
                                                                        ;
      => DbUpdate(                                                      ;
                     <(alias)>, <{key}>, <.rand.>,                      ;
                     {|| _FIELD-><f1> := <x1> [, _FIELD-><fn> := <xn>]} ;
                   )


#command JOIN  WITH <(alias)>  [TO <file>]                              ;
         [__DBFIELDLIST__ <fields>]                                     ;
         [<noopt: NOOPTIMIZE>]                                          ;
         [FOR <lfor>]                                                   ;
                                                                        ;
      => DbJoin( <(alias)>, <(file)>, <fields>, <{lfor}>,<.noopt.> )


#command JOIN <*clauses*> FIELDS <*moreclauses*> => ;
         JOIN <clauses> __DBFIELDS__ <moreclauses>

#command COUNT TO <xvar>                                                ;
         [FOR <lfor>]                                                   ;
         [WHILE <lwhile>]                                               ;
         [NEXT <nnext>]                                                 ;
         [RECORD <rec>]                                                 ;
         [<rest:REST>]                                                  ;
         [<noopt: NOOPTIMIZE>]                                          ;
         [ALL]                                                          ;
                                                                        ;
      => <xvar> := 0                                                    ;
       ; DbEval(                                                        ;
               {|| <xvar> += 1},                                        ;
               <{lfor}>, <{lwhile}>, <nnext>, <rec>, <.rest.>, <.noopt.>;
               )

#command SUM  [<x1> [, <xn>]  TO  <v1> [, <vn>] ]                       ;
         [FOR <lfor>]                                                   ;
         [WHILE <lwhile>]                                               ;
         [NEXT <nnext>]                                                 ;
         [RECORD <rec>]                                                 ;
         [<rest:REST>]                                                  ;
         [<noopt: NOOPTIMIZE>]                                          ;
         [ALL]                                                          ;
                                                                        ;
      => <v1> := [ <vn> := ] 0                                          ;
       ; DbEval(                                                        ;
               {|| <v1> += <x1> [, <vn> += <xn> ]},                     ;
               <{lfor}>, <{lwhile}>, <nnext>, <rec>, <.rest.>, <.noopt.>;
               )

#command AVERAGE   <x1> [, <xn>]  TO  <v1> [, <vn>]                     ;
         [FOR <lfor>]                                                   ;
         [WHILE <lwhile>]                                               ;
         [NEXT <nnext>]                                                 ;
         [RECORD <rec>]                                                 ;
         [<rest:REST>]                                                  ;
         [<noopt: NOOPTIMIZE>]                                          ;
         [ALL]                                                          ;
                                                                        ;
      => M->__Avg := <v1> := [ <vn> := ] 0                              ;
                                                                        ;
       ; DbEval(                                                        ;
               {|| M->__Avg += 1,                                       ;
               <v1> += <x1> [, <vn> += <xn>] },                         ;
               <{lfor}>, <{lwhile}>, <nnext>, <rec>, <.rest.>, <.noopt.>;
               )                                                        ;
                                                                        ;
       ; <v1> /= M->__Avg [; <vn> /= M->__Avg ]


// NOTE:  CLOSE <alias> must precede the others
#command CLOSE <alias>                 => <alias>->( DbCloseArea() )
#command CLOSE &<alias>                => (<alias>)->( DbCloseArea() )

#command CLOSE [<al: DATABASES,ALL>] => DbCloseAll()
#command CLOSE INDEXES                 => DbClearIndex()
#command CLOSE                         => DbCloseArea()

#endif

** EOF
